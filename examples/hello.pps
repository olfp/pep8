;
; pep8 hello world
; this example requires device 0 (teletype)
;

TTY=	0			; device id for teletype
SAR=	CLL RAR			; make shift right

RESET:	JMP	START

; the text to output is counted packed sixbit code, 
; so two characters per word preceeded by a count word

HELO:	TEXT	/Hello World!/ 	; counted packed sixbit
WCNT:	0			; counter for words

ACR:	15			; CR 
ALF:	12			; LF

AOFF:	40			; offset sixbit -> ASCII
MSK6:	77			; lower sixbit mask

PTR:	(HELO+1)		; pointer to current word

; now start

START:	RCL	HELO
	STO	WCNT		; countdown words sent

LOOP:	JMS	CHKLEN		; check if done
	RCL I	PTR		; get double-char
	SAR			; isolate high six bit
	SAR
	SAR
	SAR
	SAR
	SAR
	CLL			; asure link zero
	TAD	AOFF		; ASCII offset
	JMS	CHOUT		; send to TTY

	JMS	CHKLEN		; are we done midword?
	RCL I	PTR		; re-get double char
	AND	MSK6		; isolate lower six bit
	CLL			; asure link zero
	TAD	AOFF		; ASCII offset
	JMS	CHOUT		; send to TTY

	RCL	PTR		; advance to next word
	IAC
	STO	PTR

	JMP	LOOP

FINIS:	RCL	ACR		; send CR to TTY
	JMS	CHOUT
	RCL	ALF		; send LF to TTY
	JMS	CHOUT

	HLT			; we're done

CHKLEN:	NOP			; slot for return addr
	RCL	WCNT		; remaining length 
	SNA			; none? 
	JMP	FINIS		; then done
	CMA IAC			; else complement, increment
	CMA			; complement again 
	STO	WCNT		; now its decemeneted
	JMP I	CHKLEN		; return

CHOUT:	NOP			; slot for return addr
WAIT:	SRO	TTY		; skip if tty ready
	JMP	WAIT		; else loop
	DPA	TTY		; print char in AC
	JMP I	CHOUT		; return

; EOF
